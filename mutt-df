BEGIN {
	_threshold = 3
	if(!threshold) {
		threshold = _threshold
	}

	number_of_lines = 0

	quote_line = "> quoted block removed by display filter\n"
	len_ql = length(quote_line)
}

{ p = 1 }

/^[[:space:]]*[>|]/ {
	match($0, /^[[:space:]]*/)
	if(RLENGTH < 4 ) {
		if( $0 ~ /^[[:space:]]*[>|][[:space:]]*$/ ) {
			next
		}
		padding = RLENGTH
		p = 0
		in_block = 1
		number_of_lines++
		lines[number_of_lines] = $0
		next
	}
}

/^[[:space:]]*$/ {
	if( ! in_message_body ) {
		in_message_body = 1
	} else {
		return_block = 1
		next
	}
}

in_block {
	p = 1
	in_block = 0

	if( number_of_lines <= threshold ) {
		for( n=1; n <= number_of_lines; ++n ) {
			print( lines[n] )
		}
	} else {
		printf("%s\n", lines[1])
		printf("%*s", padding + len_ql, quote_line)
	}

	number_of_lines = 0
}

return_block {
	printf "\n"
	return_block = 0
}

p

END {
	if( in_block ) {
		printf("%*s", padding + len_ql, quote_line)
	}
}

# vim: set ft=awk:
